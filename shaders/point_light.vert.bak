#version 450

// billboard offset vertices
//  Note: these vertices are in camera space
const vec2 OFFSETS[6] = vec2[](
  vec2(-1.0, -1.0),
  vec2(-1.0, 1.0),
  vec2(1.0, -1.0),
  vec2(1.0, -1.0),
  vec2(-1.0, 1.0),
  vec2(1.0, 1.0)
);

layout (location = 0) out vec2 fragOffset;

layout(set = 0, binding = 0) uniform GlobalUbo {
	mat4 projection;
	mat4 view;
	vec4 ambientLightColor; // w is intensity
	vec3 lightPosition;
	vec4 lightColor;
} ubo;

const float LIGHT_RADIUS = 0.1;

// Note: there is an alternative method to do the initial computation
//	in camera space rather than world space.
void main() {
	fragOffset = OFFSETS[gl_VertexIndex];
	vec3 cameraRightWorld = {ubo.view[0][0], ubo.view[1][0], ubo.view[2][0]};
	vec3 cameraUpWorld =    {ubo.view[0][1], ubo.view[1][1], ubo.view[2][1]};
	
	vec4 lightCameraSpace = ubo.view * vec4(ubo.lightPosition, 1.0);
	vec4 positionCameraSpace = lightCameraSpace + LIGHT_RADIUS * vec4(fragOffset, 0.0, 0.0);
	
	vec3 positionWorld = ubo.lightPosition.xyz
		+ LIGHT_RADIUS * fragOffset.x * cameraRightWorld
		+ LIGHT_RADIUS * fragOffset.y * cameraUpWorld;
	
	gl_Position = ubo.projection * positionCameraSpace;
}